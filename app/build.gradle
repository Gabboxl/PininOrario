plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion 31
    defaultConfig {
        applicationId "ga.gabboxl.pininorario"
        minSdkVersion 19
        targetSdkVersion 31
        versionCode 160
        versionName '1.6.0'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        debug {
            versionNameSuffix "-debug"
        }

        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    flavorDimensions 'gms'
    productFlavors {
        full {
            dimension 'gms'

            apply plugin: 'com.google.gms.google-services'
            apply plugin: 'com.google.firebase.firebase-perf'
            apply plugin: 'com.google.firebase.crashlytics'
        }

        deactivatedGms {
            dimension 'gms'

            apply plugin: 'com.google.gms.google-services'
            apply plugin: 'com.google.firebase.firebase-perf'
            apply plugin: 'com.google.firebase.crashlytics'

            manifestPlaceholders = [crashlyticsCollectionEnabled: "true"]

            FirebasePerformance {
                // Set this flag to 'false' to disable @AddTrace annotation processing and
                // automatic HTTP/S network request monitoring
                // for a specific build variant at compile time.
                instrumentationEnabled false
            }

            versionNameSuffix "-deactgms"
        }

        nogms {
            dimension 'gms'

            versionNameSuffix "-nogms"
        }

    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation 'com.github.Gabboxl:PininParse:master-SNAPSHOT'

    //Firebase dependencies
    implementation 'com.google.firebase:firebase-analytics-ktx:20.0.0'
    implementation 'com.google.firebase:firebase-perf-ktx:20.0.3'
    implementation 'com.google.firebase:firebase-crashlytics-ktx:18.2.4'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.0"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2'

    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.0"

    implementation 'org.apache.commons:commons-io:1.3.2'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'com.google.android.material:material:1.5.0-alpha05'


    implementation 'com.github.GrenderG:Toasty:1.5.2'
    implementation 'com.github.gabboxl:AppUpdater:master-SNAPSHOT'
    implementation 'com.github.daniel-stoneuk:material-about-library:3.1.2'
    implementation "com.squareup.okhttp3:okhttp:4.9.2"


    implementation "com.mikepenz:iconics-core:5.2.8"
    implementation 'com.mikepenz:community-material-typeface:5.8.55.0-kotlin@aar'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}